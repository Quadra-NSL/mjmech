# Copyright 2015 Josh Pieper, jjp@pobox.com.  All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

def setup_env(env):
    bothflags = ['-Wall', '-Werror', '-g', '-fno-common']

    env.Append(CFLAGS=bothflags)
    env.Append(LINKFLAGS=['-g'])
    env.Append(CXXFLAGS=bothflags + ['-std=gnu++1y'])
    env.Append(ASFLAGS=['-x', 'assembler-with-cpp'])
    env.Append(LIBS=['m'])
    env.Append(CPPPATH=['/usr/include/eigen3'])
    env.Append(CPPPATH=['#/../../..'])
    env.Append(CPPPATH=['#'])
    env.Append(CPPDEFINES=['MJMECH_DISABLE_BOOST'])


targetenv = Environment(tools=['stm32f411'])
hostenv = Environment()

setup_env(targetenv)
setup_env(hostenv)

targetenv.Append(CPPPATH=['/usr/include/newlib/c++/4.8'])
targetenv.Append(CPPPATH=['/usr/include/newlib/c++/4.8/arm-none-eabi/armv7e-m'])

targetenv.Append(CPPPATH=[
        '#/Drivers/CMSIS/Device/ST/STM32F4xx/Include',
        '#/Drivers/CMSIS/Include',
        '#/Drivers/STM32F4xx_HAL_Driver/Inc',
        '#/Inc',
        ])
targetenv.Append(LINKFLAGS=['-Wl,--gc-sections'])
targetenv.Append(CFLAGS=['-ffunction-sections'])
targetenv.Append(CXXFLAGS=['-ffunction-sections'])
targetenv.Append(CXXFLAGS=['-fno-rtti'])
targetenv.Append(LINKFLAGS=['-fno-rtti'])

Export('targetenv')
Export('hostenv')

for subdir in [
    'Drivers/CMSIS/',
    'Drivers/STM32F4xx_HAL_Driver',
    'Middlewares',
    'Src',
    'gimbal',
    ]:
    SConscript(subdir + '/SConscript',
               variant_dir=subdir + '/build',
               duplicate=0)
