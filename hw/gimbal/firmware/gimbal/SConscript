# Copyright 2015 Josh Pieper, jjp@pobox.com.  All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

Import('hostenv')
Import('targetenv')
Import('libcmsis')
Import('libhal')
Import('libmiddlewares')
Import('libsrc')

env = targetenv.Clone()
env.Append(CXXFLAGS=['-Wno-unused-local-typedefs'])

SOURCES = [
    'gimbal.c',
    'cpp_gimbal.cc',
    'persistent_config.cc',
    'pool_ptr.cc',
    ]

HOST_SOURCES = list(set(SOURCES) - set([
            'cpp_gimbal.cc',
            'gimbal.c',
        ]))

# NOTE jpieper: There does exist a circular reference between libsrc
# and libmiddlewares.  Silly ST.
target = env.Program(
    'gimbal', SOURCES + libsrc + libmiddlewares + libsrc + libhal + libcmsis)

env.Default(target)

flash1 = env.Command(
    'gimbal.flash1.bin', target,
    '$OBJCOPY -Obinary -j .isr_vector $SOURCE $TARGET')
flash2 = env.Command(
    'gimbal.flash2.bin', target,
    '$OBJCOPY -Obinary -j .text -j .rodata -j .ARM -j .data $SOURCE $TARGET')

do_flash = env.Command(
    'flash-stamp', flash1 + flash2,
    '$FLASH write ${SOURCES[0]} 0x8000000 && ' +
    '$FLASH write ${SOURCES[1]} 0x8020000 && ' +
    'touch $TARGET')

env.Alias('flash', do_flash)

TESTS = ['test/' + x for x in [
        'persistent_config_test.cc',
        'pool_ptr_test.cc',
        'static_function_test.cc',
        'test_main.cc',
        'tokenizer_test.cc',
        ]]

testenv = hostenv.Clone()
testenv.Append(LIBS=['boost_unit_test_framework'])
testenv.Append(CXXFLAGS=['-Wno-unused-local-typedefs'])
testenv['OBJPREFIX'] = 'tst_'

test_gimbal = testenv.Program('test_gimbal', HOST_SOURCES + TESTS)
test_command = testenv.Command('test_gimbal.passed', test_gimbal,
                               '$SOURCE && touch $TARGET')

env.Default(test_command)
