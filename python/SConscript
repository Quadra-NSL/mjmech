# Copyright 2014-2015 Josh Pieper, jjp@pobox.com.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os

Import('canonenv')
Import('libbase_so')
Import('libmech_so')
env = canonenv.Clone()

PY_SOURCES = [
    'py_legtool.cc',
    'py_legtool_servo.cc',
    'py_legtool_tf.cc',
    'py_legtool_legik.cc',
    ]

bpenv = env.Clone()
bpenv.Append(LIBS=['boost_python'])


pytelemetry = bpenv.SharedLibrary(
    '_telemetry_archive', ['py_telemetry_archive.cc'], LIBPREFIX='')


def add_lib(lib):
    path = lib[0].Dir('.').abspath
    relpath = os.path.relpath(path, Dir('.').abspath)
    bpenv.Append(LIBPATH=[path])
    libname = str(lib[0].name).split('.', 1)[0]
    if libname.startswith('lib'):
        libname = libname[3:]
    bpenv.Append(LIBS=[libname])
    bpenv.Append(LINKFLAGS=['-Wl,-rpath=\\$$ORIGIN/' + relpath])

add_lib(libmech_so)
add_lib(libbase_so)
bpenv.ParseConfig('pkg-config --cflags --libs python')
pylegtool = bpenv.SharedLibrary(
    '_legtool', PY_SOURCES,
    LIBPREFIX='')
