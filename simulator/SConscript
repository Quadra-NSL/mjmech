# Copyright 2014-2015 Josh Pieper, jjp@pobox.com.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import subprocess

Import('gsenv')
Import('libfmt')
Import('libbase')
Import('libmech')

env = gsenv.Clone()
env.Append(CPPPATH=['.'])

PKGS = [
    'dart',
    'assimp',
    ]


rpaths = set()

env['PKG_CONFIG_PATH'] = '/opt/dart-5/lib/pkgconfig'

for pkg in PKGS:
    env.ParseConfig('PKG_CONFIG_PATH=$PKG_CONFIG_PATH '
                    'pkg-config --cflags-only-I --libs %s' % pkg)
    # TODO theamk: is there a more scons'y way to do this?
    libdirs = subprocess.check_output(
        'PKG_CONFIG_PATH=%s pkg-config --libs-only-L %s' % (
            env['PKG_CONFIG_PATH'], pkg), shell=True).strip()
    for libdir in libdirs.split(' '):
        if libdir == '':
            continue
        assert libdir.startswith('-L')
        rpaths.add(libdir.split('-L', 1)[1])

for rpath in rpaths:
    env.Append(LINKFLAGS=['-Wl,-rpath=%s' % rpath])

# TODO theamk: can the block above be simply replaced by line below?
#  env.append(RPATH="/opt/dart-5/lib")


env.Append(LIBS=['dart-core', 'glut'])

SOURCES = [
    'herkulex_protocol.cc',
    'simulator_window.cc',
    ]

sim_objects = env.Object(SOURCES)

simulator = env.Program(
    'simulator', ['simulator.cc'] + SOURCES + libmech + libbase + libfmt)
