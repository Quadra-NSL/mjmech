# Copyright 2014-2015 Josh Pieper, jjp@pobox.com.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import subprocess

Import('canonenv')
Import('libbase')
Import('libmech')

env = canonenv.Clone()
env.Append(CPPPATH=['.'])

PKGS = [
    'dart',
    'assimp',
    ]


rpaths = set()


for pkg in PKGS:
    env.ParseConfig('pkg-config --cflags-only-I --libs %s' % pkg)
    libdirs = subprocess.check_output(
        'pkg-config --libs-only-L %s' % pkg, shell=True).strip()
    for libdir in libdirs.split(' '):
        if libdir == '':
            continue
        assert libdir.startswith('-L')
        rpaths.add(libdir.split('-L', 1)[1])

for rpath in rpaths:
    env.Append(LINKFLAGS=['-Wl,-rpath=%s' % rpath])


env.Append(LIBS=['dart-core', 'glut'])

SOURCES = [
    'herkulex_protocol.cc',
    'simulator_window.cc',
    ]

sim_objects = env.Object(SOURCES)

simulator = env.Program(
    'simulator', ['simulator.cc'] + SOURCES + libmech + libbase)
