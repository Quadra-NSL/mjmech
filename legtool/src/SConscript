# Copyright 2014-2015 Josh Pieper, jjp@pobox.com.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

Import('canonenv')
env = canonenv.Clone()

env.Append(CPPPATH=['.'])
env.ParseConfig('pkg-config --cflags --libs eigen3')
env.Append(LIBS=['boost_system',
                 'boost_program_options',
                 'boost_coroutine',
                 'boost_context',
                 'pthread'])

SOURCES = [
    'comm_factory.cc',
    ]

liblegtool = env.StaticLibrary('legtool', SOURCES)

TESTS = ['test/' + x for x in [
        'test_comm.cc',
        'test_herkulex.cc',
        'test_leg_ik.cc',
        'test_main.cc',
        'test_property_tree_archive.cc',
        'test_quaternion.cc',
        'test_ripple.cc',
        'test_signal_result.cc',
        'test_tf.cc',
        ]]

testenv = env.Clone()
testenv.Append(LIBS=['boost_unit_test_framework'])

test_legtool = testenv.Program('test_legtool', TESTS + liblegtool)
testenv.Command('test_legtool.passed', test_legtool,
                '$SOURCE && touch $TARGET')

comm_manual_test = env.Program('comm_manual_test',
                               ['test/comm_manual_test.cc'] + liblegtool)
