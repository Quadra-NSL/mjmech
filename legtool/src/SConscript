# Copyright 2014-2015 Josh Pieper, jjp@pobox.com.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

Import('canonenv')
env = canonenv.Clone()

env.Append(CPPPATH=['.'])
env.ParseConfig('pkg-config --cflags --libs eigen3')
env.Append(LIBS=['boost_system',
                 'boost_program_options',
                 'pthread'])

crenv = env.Clone()
crenv.Append(LIBS=['boost_coroutine',
                   'boost_context',
                   ])

SOURCES = [
    'comm_factory.cc',
    'herkulex.cc',
    'telemetry.cc',
    ]

liblegtool = crenv.StaticLibrary('legtool', SOURCES)

herkulex_tool = crenv.Program('herkulex_tool',
                            ['herkulex_tool.cc'] + liblegtool)

telemetry_tool = crenv.Program('telemetry_tool',
                               ['telemetry_tool.cc'])

bpenv = env.Clone()
bpenv.Append(LIBS=['boost_python'])
bpenv.ParseConfig('pkg-config --cflags --libs python')
pylegtool = bpenv.SharedLibrary('_legtool', SOURCES + ['py_legtool.cc'],
                                LIBPREFIX='')

TESTS = ['test/' + x for x in [
        'comm_test.cc',
        'herkulex_test.cc',
        'leg_ik_test.cc',
        'test_main.cc',
        'property_tree_archive_test.cc',
        'quaternion_test.cc',
        'ripple_test.cc',
        'signal_result_test.cc',
        'telemetry_archive_test.cc',
        'tf_test.cc',
        ]]

testenv = crenv.Clone()
testenv.Append(LIBS=['boost_unit_test_framework'])

test_legtool = testenv.Program('test_legtool', TESTS + liblegtool)
testenv.Command('test_legtool.passed', test_legtool,
                '$SOURCE && touch $TARGET')

comm_manual_test = crenv.Program('comm_manual_test',
                                 ['test/comm_manual_test.cc'] + liblegtool)
